#include <SPI.h>
#include <WiFiNINA.h>
#include "arduino_secrets.h" 

//=====================================
//==        WiFI Vars                ==
//=====================================
char ssid[] = SECRET_SSID;        // your network SSID (name)
char pass[] = SECRET_PASS;        // your network password (use for WPA, or use as key for WEP)
int status = WL_IDLE_STATUS;      // the Wifi radio's status

boolean lastConnected = false;  
char server[] = "api.pushingbox.com";
WiFiClient client;

//=====================================
//==        Pushing Box Vars         ==
//=====================================
char DEVICE_ID_UPLOAD[] = UPLOAD_ID;    //Provided by the pushingbox api
char SS_ID[]     = SPREADSHEET_ID;               //Unique id of google spreadsheet
char TAB_ID[]    = PAGE_ID;              //Determines tab where data is uploaded

char DEVICE_ID_MAIL[] = MAIL_ID;        //Provided by the pushingbox api

boolean DEBUG    = true;

//=====================================
//==        Data Vars                ==
//=====================================
const int ldrPin1 = A0;
const int ldrPin2 = A1;
const int ldrPin3 = A2;

const int ledPin1 = 8;
const int ledPin2 = 9;
const int ledPin3 = 10;

boolean stat1 = true;
boolean stat2 = false;
boolean stat3 = false;

int repCounter = 0;

unsigned long t1 = 0;
unsigned long t2 = 0;

long duration = 0;

const long TIMEOUT = 300000;

//--------------------------------------------------------------------------

void setup() {
  //=====================================
  //==        Data Setup               ==
  //=====================================
  pinMode(ledPin1, OUTPUT);
  pinMode(ledPin2, OUTPUT);
  pinMode(ledPin3, OUTPUT);

  pinMode(ldrPin1,INPUT);
  pinMode(ldrPin2,INPUT);
  pinMode(ldrPin3,INPUT);
  
  //====================================================
  //==   Wifi Code (Straight From WiFiNina Example)   ==
  //====================================================
  Serial.begin(9600);
  while (!Serial) {
    ; // wait for serial port to connect. Needed for native USB port only
  }

  // check for the WiFi module:
  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println("Communication with WiFi module failed!");
    // don't continue
    while (true);
  }

  String fv = WiFi.firmwareVersion();
  if (fv < WIFI_FIRMWARE_LATEST_VERSION) {
    Serial.println("Please upgrade the firmware");
  }

  // attempt to connect to Wifi network:
  while (status != WL_CONNECTED) {
    if (DEBUG){
      Serial.print("Attempting to connect to WPA SSID: ");
      Serial.println(ssid);
    }
    // Connect to WPA/WPA2 network:
    status = WiFi.begin(ssid, pass);

    // wait 10 seconds for connection:
    delay(10000);
  }

  if (DEBUG) {
    Serial.print("You're connected to the network");
    printWifiStatus();
  }

  //=====================================
  //==        Notification             ==
  //=====================================
  notifyPushingBox();
  
}

//--------------------------------------------------------------------------

void loop() {  
  //=====================================
  //==        Data Collection          ==
  //=====================================
  int ldrStatus1 = analogRead(ldrPin1);
  int ldrStatus2 = analogRead(ldrPin2);
  int ldrStatus3 = analogRead(ldrPin3);

  if (ldrStatus1 <= 200) {
    digitalWrite(ledPin1, HIGH);
    stat1 = true;
    delay(1000);
  } else {
    digitalWrite(ledPin1, LOW);
  }
  if (ldrStatus2 <= 200) {
    digitalWrite(ledPin2, HIGH);
    stat2 = true;
    delay(1000);
  } 
  else {
    digitalWrite(ledPin2, LOW);
  } if (ldrStatus3 <= 200) {
    digitalWrite(ledPin3, HIGH);
    stat3 = true;
    delay(1000);
  } 
  else {digitalWrite(ledPin3, LOW);
  }

  if (repCounter > 0) {t2 = millis();}

  if (stat1 == true && stat2 == true && stat3 == true ){
    repCounter +=1; 
    stat1 = false;
    stat2 = false;
    stat3 = false;

    if (repCounter > 0) {
      duration += (t2 - t1);
    }

    t1 = millis();
  }

  //=====================================
  //==        Upload Interval          ==
  //=====================================
  //Data sent once 8 minutes have passed from the last recorded "rep."
  if (repCounter > 0 && (t2 - t1) > TIMEOUT) {
    sendToPushingBox();
    repCounter = 0;
    duration = 0;
  }

  //=====================================
  //==        Debug                    ==
  //=====================================
  // This writes a response from the PushingBox Server.
  // You should see a "200 OK"
  if (client.available()) {
    char c = client.read();
    if(DEBUG){Serial.print(c);}
  }
 
  // if there's no net connection, but there was one last time
  // through the loop, then stop the client:
  if (!client.connected() && lastConnected) {
    if(DEBUG){Serial.println();}
    if(DEBUG){Serial.println("disconnecting.");}
    client.stop();
  }
  lastConnected = client.connected();
}

//--------------------------------------------------------------------------

//=====================================
//==        Send Data                ==
//=====================================
//Function for sending the request to PushingBox
void sendToPushingBox(){
  client.stop(); 
  if(DEBUG){Serial.println("Connecting...");}
  if(client.connect(server, 80)) { 
    if(DEBUG){Serial.println("Connected");}
    if(DEBUG){Serial.println("Sending Request");}

    char head[1024];
    sprintf(head, "GET /pushingbox?devid=%s&key0=sheetID&val0=%s&key1=tabID&val1=%s&key2=full_data&val2=", 
    DEVICE_ID_UPLOAD,     // PushingBox Device ID
    SS_ID,         // Spreadsheet ID
    TAB_ID);       // Tab or Sheet Selected

    duration /= (60000); //Converts elapsed duration to minutes

    char msg[1024];
    sprintf(msg, "Reps~%d~Duration_(Mins)~%d", repCounter, duration);

    client.print(head);
    client.print(msg);
    client.println(" HTTP/1.1\nHost: api.pushingbox.com\nUser-Agent: Arduino\n");

    if(DEBUG) {
      Serial.print(head);
      Serial.print(msg);
      Serial.println(" HTTP/1.1\nHost: api.pushingbox.com\nUser-Agent: Arduino\n");
    }
  } else { if(DEBUG){Serial.println("connection failed");}} 
}

//=====================================
//==        Notify                   ==
//=====================================
void notifyPushingBox(){
  client.stop(); 
  if(DEBUG){Serial.println("Connecting...");}
  if(client.connect(server, 80)) { 
    if(DEBUG){Serial.println("Connected");}
    if(DEBUG){Serial.println("Sending Request");}

    char head[1024];
    sprintf(head, "GET /pushingbox?devid=%s", 
    DEVICE_ID_MAIL);     // PushingBox Device ID
    
    client.print(head);
    client.println(" HTTP/1.1\nHost: api.pushingbox.com\nUser-Agent: Arduino\n");

    if(DEBUG) {
      Serial.print(head);
      Serial.println(" HTTP/1.1\nHost: api.pushingbox.com\nUser-Agent: Arduino\n");
    }
  } else { if(DEBUG){Serial.println("connection failed");}}   
}

//=====================================
//==        Wifi Debug               ==
//=====================================
void printWifiStatus() {
  // print the SSID of the network you're attached to:
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());

  // print your board's IP address:
  IPAddress ip = WiFi.localIP();
  Serial.print("IP Address: ");
  Serial.println(ip);

  // print the received signal strength:
  long rssi = WiFi.RSSI();
  Serial.print("signal strength (RSSI):");
  Serial.print(rssi);
  Serial.println(" dBm");
}
